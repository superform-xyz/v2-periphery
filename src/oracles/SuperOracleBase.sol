// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.30;

// external
import { IOracle } from "../vendor/awesome-oracles/IOracle.sol";
import { AggregatorV3Interface } from "../vendor/chainlink/AggregatorV3Interface.sol";
import { IERC20 } from "forge-std/interfaces/IERC20.sol";
import { BoringERC20 } from "../vendor/BoringSolidity/BoringERC20.sol";
import { Math } from "@openzeppelin/contracts/utils/math/Math.sol";

// Superform
import { ISuperOracle } from "../interfaces/oracles/ISuperOracle.sol";

/// @title SuperOracle
/// @author Superform Labs
/// @notice Oracle for Superform
abstract contract SuperOracleBase is ISuperOracle, IOracle {
    using BoringERC20 for IERC20;

    /// @notice Mapping of feed to max staleness period
    mapping(address feed => uint256 maxStaleness) public feedMaxStaleness;

    /// @notice Mapping of token to emergency price when oracle is down
    mapping(address token => uint256 emergencyPrice) public emergencyPrices;

    uint256 public maxDefaultStaleness;

    /// @notice Pending oracle update
    PendingUpdate public pendingUpdate;

    /// @notice Pending provider removal
    PendingRemoval public pendingRemoval;

    /// @notice Mapping of base asset to array of oracle providers to oracle feed address
    mapping(address base => mapping(address quote => mapping(bytes32 provider => address feed))) internal oracles;

    /// @notice Array of active provider ids
    bytes32[] public activeProviders;
    mapping(bytes32 provider => bool isSet) public isProviderSet;

    /// @notice Timelock period for oracle updates
    uint256 internal constant TIMELOCK_PERIOD = 1 weeks;
    uint256 internal constant MAX_SAMPLE_PROVIDERS = 10;
    bytes32 internal constant AVERAGE_PROVIDER = keccak256("AVERAGE_PROVIDER");

    // SuperGovernor address
    address public immutable SUPER_GOVERNOR;

    constructor(
        address superGovernor_,
        address[] memory bases,
        address[] memory quotes,
        bytes32[] memory providers,
        address[] memory feeds
    ) {
        if (superGovernor_ == address(0)) revert ZERO_ADDRESS();
        SUPER_GOVERNOR = superGovernor_;
        maxDefaultStaleness = 1 days;

        // validate oracle inputs
        _validateOracleInputs(bases, quotes, providers, feeds);

        // configure oracles
        _configureOracles(bases, quotes, providers, feeds);

        // set default staleness for feeds
        uint256 length = feeds.length;
        for (uint256 i; i < length; ++i) {
            feedMaxStaleness[feeds[i]] = maxDefaultStaleness;
        }
    }

    /*//////////////////////////////////////////////////////////////
                            EXTERNAL FUNCTIONS
    //////////////////////////////////////////////////////////////*/
    /// @inheritdoc ISuperOracle
    function setMaxStaleness(uint256 newMaxStaleness) external {
        if (msg.sender != SUPER_GOVERNOR) revert UNAUTHORIZED_UPDATE_AUTHORITY();
        maxDefaultStaleness = newMaxStaleness;
        emit MaxStalenessUpdated(newMaxStaleness);
    }

    /// @inheritdoc ISuperOracle
    function setFeedMaxStaleness(address feed, uint256 newMaxStaleness) external {
        if (msg.sender != SUPER_GOVERNOR) revert UNAUTHORIZED_UPDATE_AUTHORITY();
        _setFeedMaxStaleness(feed, newMaxStaleness);
    }

    /// @inheritdoc ISuperOracle
    function setEmergencyPrice(address token_, uint256 price_) external {
        if (msg.sender != SUPER_GOVERNOR) revert UNAUTHORIZED_UPDATE_AUTHORITY();
        _setEmergencyPrice(token_, price_);
    }

    /// @inheritdoc ISuperOracle
    function batchSetEmergencyPrice(address[] calldata tokens_, uint256[] calldata prices_) external {
        if (msg.sender != SUPER_GOVERNOR) revert UNAUTHORIZED_UPDATE_AUTHORITY();
        uint256 length = tokens_.length;
        if (length == 0) revert ZERO_ARRAY_LENGTH();
        if (length != prices_.length) revert ARRAY_LENGTH_MISMATCH();

        for (uint256 i; i < length; ++i) {
            _setEmergencyPrice(tokens_[i], prices_[i]);
        }
    }

    /// @inheritdoc ISuperOracle
    function setFeedMaxStalenessBatch(address[] calldata feeds, uint256[] calldata newMaxStalenessList) external {
        if (msg.sender != SUPER_GOVERNOR) revert UNAUTHORIZED_UPDATE_AUTHORITY();
        uint256 length = feeds.length;
        if (length == 0) revert ZERO_ARRAY_LENGTH();
        if (length != newMaxStalenessList.length) {
            revert ARRAY_LENGTH_MISMATCH();
        }

        for (uint256 i; i < length; ++i) {
            _setFeedMaxStaleness(feeds[i], newMaxStalenessList[i]);
        }
    }

    /// @inheritdoc ISuperOracle
    function queueOracleUpdate(
        address[] calldata bases,
        address[] calldata quotes,
        bytes32[] calldata providers,
        address[] calldata feeds
    )
        external
    {
        if (msg.sender != SUPER_GOVERNOR) revert UNAUTHORIZED_UPDATE_AUTHORITY();
        uint256 length = bases.length;
        if (length != quotes.length || length != providers.length || length != feeds.length) {
            revert ARRAY_LENGTH_MISMATCH();
        }

        _validateOracleInputs(bases, quotes, providers, feeds);

        pendingUpdate = PendingUpdate({
            bases: bases,
            quotes: quotes,
            providers: providers,
            feeds: feeds,
            timestamp: block.timestamp
        });

        emit OracleUpdateQueued(bases, quotes, providers, feeds, block.timestamp);
    }

    /// @inheritdoc ISuperOracle
    function executeOracleUpdate() external {
        if (pendingUpdate.timestamp == 0) revert NO_PENDING_UPDATE();
        if (block.timestamp < pendingUpdate.timestamp + TIMELOCK_PERIOD) revert TIMELOCK_NOT_ELAPSED();

        _configureOracles(pendingUpdate.bases, pendingUpdate.quotes, pendingUpdate.providers, pendingUpdate.feeds);

        emit OracleUpdateExecuted(
            pendingUpdate.bases, pendingUpdate.quotes, pendingUpdate.providers, pendingUpdate.feeds
        );

        delete pendingUpdate;
    }

    /// @inheritdoc ISuperOracle
    function getOracleAddress(address base, address quote, bytes32 provider) external view returns (address oracle) {
        oracle = oracles[base][quote][provider];
        if (oracle == address(0)) revert NO_ORACLES_CONFIGURED();
        if (!isProviderSet[provider]) return address(0);
    }

    /// @inheritdoc ISuperOracle
    function queueProviderRemoval(bytes32[] calldata providers) external {
        if (msg.sender != SUPER_GOVERNOR) revert UNAUTHORIZED_UPDATE_AUTHORITY();
        if (pendingRemoval.timestamp != 0) revert PENDING_UPDATE_EXISTS();

        uint256 length = providers.length;
        if (length == 0) revert ZERO_ARRAY_LENGTH();

        pendingRemoval = PendingRemoval({ providers: providers, timestamp: block.timestamp });

        emit ProviderRemovalQueued(providers, block.timestamp);
    }

    /// @inheritdoc ISuperOracle
    function executeProviderRemoval() external {
        if (pendingRemoval.timestamp == 0) revert NO_PENDING_UPDATE();
        if (block.timestamp < pendingRemoval.timestamp + TIMELOCK_PERIOD) revert TIMELOCK_NOT_ELAPSED();

        bytes32[] memory providersToRemove = pendingRemoval.providers;

        // Loop through each provider to remove
        for (uint256 i; i < providersToRemove.length; i++) {
            bytes32 providerToRemove = providersToRemove[i];
            isProviderSet[providerToRemove] = false;

            // Find the provider in activeProviders array
            for (uint256 j; j < activeProviders.length; j++) {
                if (activeProviders[j] == providerToRemove) {
                    // Replace the provider to remove with the last provider in the array
                    if (j < activeProviders.length - 1) {
                        activeProviders[j] = activeProviders[activeProviders.length - 1];
                    }

                    // Remove the last element
                    activeProviders.pop();
                    break;
                }
            }
        }

        emit ProviderRemovalExecuted(providersToRemove);

        delete pendingRemoval;
    }

    /// @inheritdoc ISuperOracle
    function getActiveProviders() external view returns (bytes32[] memory) {
        return activeProviders;
    }

    /*//////////////////////////////////////////////////////////////
                        EXTERNAL VIEW FUNCTIONS
    //////////////////////////////////////////////////////////////*/
    /// @inheritdoc ISuperOracle
    function getQuoteFromProvider(
        uint256 baseAmount,
        address base,
        address quote,
        bytes32 oracleProvider
    )
        public
        view
        virtual
        returns (uint256 quoteAmount, uint256 deviation, uint256 totalProviders, uint256 availableProviders)
    {
        // If average, calculate average of all oracles
        if (oracleProvider == AVERAGE_PROVIDER) {
            uint256 length = activeProviders.length;
            uint256[] memory validQuotes = new uint256[](length);
            uint256 count;
            (quoteAmount, validQuotes, totalProviders, count) = _getAverageQuote(base, quote, baseAmount, length);
            availableProviders = count;
            deviation = _calculateStdDev(validQuotes, count);
        } else {
            quoteAmount = _getQuoteFromOracle(oracles[base][quote][oracleProvider], baseAmount, base, quote, true);
            deviation = 0;
            totalProviders = 1;
            availableProviders = 1;
        }
    }

    /// @inheritdoc ISuperOracle
    function getEmergencyPrice(address token) external view returns (uint256) {
        return emergencyPrices[token];
    }

    /// @inheritdoc IOracle
    function getQuote(
        uint256 baseAmount,
        address base,
        address quote
    )
        external
        view
        virtual
        returns (uint256 quoteAmount)
    {
        // using IOracle interface we always assume average provider
        (quoteAmount,,,) = getQuoteFromProvider(baseAmount, base, quote, AVERAGE_PROVIDER);
    }

    /*//////////////////////////////////////////////////////////////
                            INTERNAL FUNCTIONS
    //////////////////////////////////////////////////////////////*/
    function _validateOracleInputs(
        address[] memory bases,
        address[] memory quotes,
        bytes32[] memory providers,
        address[] memory feeds
    )
        internal
        pure
    {
        uint256 length = bases.length;
        for (uint256 i; i < length; ++i) {
            address base = bases[i];
            address quote = quotes[i];
            bytes32 provider = providers[i];
            address feed = feeds[i];

            if (provider == bytes32(0)) revert ZERO_PROVIDER();
            if (provider == AVERAGE_PROVIDER) revert AVERAGE_PROVIDER_NOT_ALLOWED();
            if (base == address(0) || quote == address(0) || feed == address(0)) revert ZERO_ADDRESS();
        }
    }

    function _setFeedMaxStaleness(address feed, uint256 newMaxStaleness) internal {
        if (newMaxStaleness > maxDefaultStaleness) {
            revert MAX_STALENESS_EXCEEDED();
        }
        if (newMaxStaleness == 0) {
            newMaxStaleness = maxDefaultStaleness;
        }
        feedMaxStaleness[feed] = newMaxStaleness;
        emit FeedMaxStalenessUpdated(feed, newMaxStaleness);
    }

    function _getQuoteFromOracle(
        address oracle,
        uint256 baseAmount,
        address base,
        address quote,
        bool revertOnError
    )
        internal
        view
        virtual
        returns (uint256 quoteAmount)
    {
        int256 answer;
        uint256 updatedAt;

        // --- Get round data ---
        try AggregatorV3Interface(oracle).latestRoundData() returns (
            uint80, int256 _answer, uint256, uint256 _updatedAt, uint80
        ) {
            answer = _answer;
            updatedAt = _updatedAt;
        } catch {
            if (revertOnError) revert ORACLE_ROUND_DATA_CALL_FAIL(oracle);
            return 0;
        }

        // --- Validate data ---
        if (answer <= 0 || block.timestamp - updatedAt > feedMaxStaleness[oracle]) {
            if (revertOnError) revert ORACLE_UNTRUSTED_DATA();
            return 0;
        }

        // --- Get decimals and compute scaled amount ---
        try AggregatorV3Interface(oracle).decimals() returns (uint8 feedDecimals) {
            uint8 baseDecimals = IERC20(base).safeDecimals();
            uint8 quoteDecimals = IERC20(quote).safeDecimals();

            // Calculate quote amount with proper decimal scaling
            quoteAmount = Math.mulDiv(baseAmount, uint256(answer), 10 ** feedDecimals);
            quoteAmount = Math.mulDiv(quoteAmount, 10 ** quoteDecimals, 10 ** baseDecimals);
        } catch {
            if (revertOnError) revert ORACLE_DECIMALS_CALL_FAIL(oracle);
            return 0;
        }
    }


    function _getAverageQuote(
        address base,
        address quote,
        uint256 baseAmount,
        uint256 numberOfProviders
    )
        internal
        view
        virtual
        returns (uint256 quoteAmount, uint256[] memory validQuotes, uint256 totalCount, uint256 count)
    {
        uint256 total;
        validQuotes = new uint256[](numberOfProviders);

        // Loop through all active providers
        // Iterates only until MAX_SAMPLE_PROVIDERS valid quotes are collected to bound gas usage
        for (uint256 i; i < numberOfProviders; ++i) {
            bytes32 provider = activeProviders[i];
            address providerOracle = oracles[base][quote][provider];
            // provider is in active list but has no available oracle address
            /*
            base = ETH
            quote = [USD, EUR]
            providers = [CHAINLINK, eORACLE]

            Let's say we have

            ETH -> USD -> CHAINLINK -> address(0x1)
            ETH -> USD - eORACLE -> address(0x2)
            ETH -> EUR -> CHAINLINK -> address(0x3)

            This would just continue for 

            ETH -> EUR -> eOracle, because oracle address is 0
            */
            if (providerOracle == address(0)) continue;

            // We have one more registered oracle for this base asset
            unchecked {
                ++totalCount;
            }

            uint256 quote_ = _getQuoteFromOracle(providerOracle, baseAmount, base, quote, false);

            /// @dev we don't revert on error, we just skip the oracle value
            if (quote_ > 0) {
                total += quote_;
                validQuotes[count] = quote_;
                // This oracle is available
                unchecked {
                    ++count;
                }
                if (count == MAX_SAMPLE_PROVIDERS) {
                    break;
                }
            }
        }
        if (count == 0) revert NO_VALID_REPORTED_PRICES();

        quoteAmount = total / count;
    }

    function _getOracleDecimals(AggregatorV3Interface oracle_) internal view virtual returns (uint8) {
        return oracle_.decimals();
    }

    function _calculateStdDev(uint256[] memory values, uint256 length) internal pure virtual returns (uint256 stddev) {
        uint256 sum = 0;
        uint256 count = 0;
        for (uint256 i; i < length; ++i) {
            sum += values[i];
            count++;
        }
        if (count < 2) return 0;

        uint256 mean = sum / count;
        uint256 sumSquaredDiff = 0;
        for (uint256 i; i < length; ++i) {
            uint256 diff;
            if (values[i] >= mean) {
                diff = values[i] - mean;
            } else {
                diff = mean - values[i];
            }

            uint256 squaredDiff = Math.mulDiv(diff, diff, 1);
            sumSquaredDiff += squaredDiff;
        }

        uint256 variance = sumSquaredDiff / count;
        return _sqrt(variance);
    }

    function _sqrt(uint256 x) internal pure returns (uint256 y) {
        if (x == 0) return 0;

        uint256 z = (x + 1) / 2;
        y = x;

        while (z < y) {
            y = z;
            z = (x / z + z) / 2;
        }
    }

    function _configureOracles(
        address[] memory bases,
        address[] memory quotes,
        bytes32[] memory providers,
        address[] memory feeds
    )
        internal
    {
        uint256 length = bases.length;

        for (uint256 i; i < length; ++i) {
            address base = bases[i];
            address quote = quotes[i];
            bytes32 provider = providers[i];
            address feed = feeds[i];

            oracles[base][quote][provider] = feed;

            // Update activeProviders array - add provider if not already present
            bool providerExists = false;
            uint256 activeProvidersLength = activeProviders.length;
            for (uint256 j; j < activeProvidersLength; ++j) {
                if (activeProviders[j] == provider) {
                    providerExists = true;
                    break;
                }
            }

            if (!providerExists) {
                activeProviders.push(provider);
                isProviderSet[provider] = true;
            }
        }
    }

    /// @notice Internal logic to set an emergency price for a token.
    /// @param token_ The address of the token.
    /// @param price_ The emergency price to set.
    function _setEmergencyPrice(address token_, uint256 price_) internal {
        emergencyPrices[token_] = price_;
        emit EmergencyPriceUpdated(token_, price_);
    }
}
